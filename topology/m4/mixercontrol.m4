divert(-1)

dnl Define macro for mixer control

dnl KCONTROL_CHANNEL(name, reg, shift)
define(`KCONTROL_CHANNEL',
`channel.STR($1) {'
`		reg STR($2)'
`		shift STR($3)'
`	}')

dnl CONTROLMIXER_MAX(comment, value)
define(`CONTROLMIXER_MAX',
`#$1'
`	max STR($2)')

dnl CONTROLMIXER_TLV(comment, value)
define(`CONTROLMIXER_TLV',
`#$1'
`	tlv STR($2)')

dnl CONTROLMIXER_OPS(info, comment, get, put)
define(`CONTROLMIXER_OPS',
`ops."ctl" {'
`		info STR($1)'
`		#$2'
`		get STR($3)'
`		put STR($4)'
`	}')

dnl KCONTROL_MONO_CHANNEL(reg)
define(`KCONTROL_MONO_CHANNEL',
`KCONTROL_CHANNEL(MONO, $1, 0)')

dnl KCONTROL_STEREO_CHANNELS(reg)
define(`KCONTROL_STEREO_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1)')

dnl KCONTROL_THREE_CHANNELS(reg)
define(`KCONTROL_THREE_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1),'
`KCONTROL_CHANNEL(FC, $1, 2)')

dnl KCONTROL_FOUR_CHANNELS(reg)
define(`KCONTROL_FOUR_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1),'
`KCONTROL_CHANNEL(RL, $1, 2),'
`KCONTROL_CHANNEL(RR, $1, 3)')

dnl KCONTROL_FIVE_CHANNELS(reg)
define(`KCONTROL_FIVE_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1),'
`KCONTROL_CHANNEL(RL, $1, 2),'
`KCONTROL_CHANNEL(RR, $1, 3),'
`KCONTROL_CHANNEL(FC, $1, 4)')

dnl KCONTROL_SIX_CHANNELS(reg)
define(`KCONTROL_SIX_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1),'
`KCONTROL_CHANNEL(RL, $1, 2),'
`KCONTROL_CHANNEL(RR, $1, 3),'
`KCONTROL_CHANNEL(FC, $1, 4),'
`KCONTROL_CHANNEL(LFE, $1, 5)')

KCONTROL_SEVEN_CHANNELS(reg)
define(`KCONTROL_SEVEN_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1),'
`KCONTROL_CHANNEL(RL, $1, 2),'
`KCONTROL_CHANNEL(RR, $1, 3),'
`KCONTROL_CHANNEL(FC, $1, 4),'
`KCONTROL_CHANNEL(SL, $1, 5),'
`KCONTROL_CHANNEL(SR, $1, 6)')

dnl KCONTROL_EIGHT_CHANNELS(reg)
define(`KCONTROL_EIGHT_CHANNELS',
`KCONTROL_CHANNEL(FL, $1, 0),'
`KCONTROL_CHANNEL(FR, $1, 1),'
`KCONTROL_CHANNEL(RL, $1, 2),'
`KCONTROL_CHANNEL(RR, $1, 3),'
`KCONTROL_CHANNEL(FC, $1, 4),'
`KCONTROL_CHANNEL(LFE, $1, 5),'
`KCONTROL_CHANNEL(SL, $1, 6),'
`KCONTROL_CHANNEL(SR, $1, 7)')

dnl KCONTROL_CHANNEL_ARRAY(size, reg)
define(`KCONTROL_CHANNEL_ARRAY',
`ifelse(
        $1, `1', `KCONTROL_MONO_CHANNEL($2)',
        $1, `2', `KCONTROL_STEREO_CHANNELS($2)',
        $1, `3', `KCONTROL_THREE_CHANNELS($2)',
        $1, `4', `KCONTROL_FOUR_CHANNELS($2)',
        $1, `5', `KCONTROL_FIVE_CHANNELS($2)',
        $1, `6', `KCONTROL_SIX_CHANNELS($2)',
        $1, `7', `KCONTROL_SEVEN_CHANNELS($2)',
        $1, `8', `KCONTROL_EIGHT_CHANNELS($2)',
        `KCONTROL_STEREO_CHANNELS($2)')')

dnl C_CONTROLMIXER(name, index, ops, max, invert, tlv, KCONTROL_CHANNELS)
define(`C_CONTROLMIXER',
`SectionControlMixer."$1 PIPELINE_ID" {'
`'
`	# control belongs to this index group'
`	index STR($2)'
`'
`	#$7'
`	$8'
`	# control uses bespoke driver get/put/info ID'
`	$3'
`'
`	$4'
`	invert STR($5)'
`	$6'

`}')

divert(0)dnl
